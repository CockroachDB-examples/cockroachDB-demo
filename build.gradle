buildscript {
    dependencies {
        classpath("org.flywaydb:flyway-database-postgresql:${flywayVersion}")
    }
}

plugins {
    id 'application'
    id "com.github.ben-manes.versions" version "${versionPluginVersion}"
    id "org.flywaydb.flyway" version "${flywayVersion}"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.postgresql:postgresql:${postgresVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

def cockroachProperties = [
           "-DcockroachDBCluster=${cockroachDBCluster}",
           "-DcockroachDBPort=${cockroachDBPort}",
           "-DcockroachDBDatabase=${cockroachDBDatabase}",
           "-DcockroachDBUser=${cockroachDBUser}",
           "-DcockroachDBPassword=${cockroachDBPassword}"
]
application {
    mainClass = 'com.example.cockroachdb.App'
    applicationDefaultJvmArgs = cockroachProperties
}

run.dependsOn flywayMigrate
test.dependsOn flywayMigrate

flyway {
    url = "jdbc:postgresql://${cockroachDBCluster}:${cockroachDBPort}/${cockroachDBDatabase}?sslmode=verify-full"
    user = "${cockroachDBUser}"
    password = "${cockroachDBPassword}"
}

test {
    useJUnitPlatform()
    jvmArgs = cockroachProperties
}